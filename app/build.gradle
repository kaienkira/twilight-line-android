apply plugin: "com.android.application"

def keystorePropertiesFile = project.file("key/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.brickredstudio.twilightline"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "0.1.0"

        manifestPlaceholders = [
            appName: "Twilight Line"
        ]

        externalNativeBuild {
            ndkBuild {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                arguments "-j${Runtime.runtime.availableProcessors()}"
            }
        }
    }

    signingConfigs {
        release {
            v1SigningEnabled true
            v2SigningEnabled true
            storeFile project.file("key/android.keystore")
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    dependencies {
        implementation "androidx.appcompat:appcompat:1.2.0"
    }

    lintOptions {
        disable "GoogleAppIndexingWarning",
                "IconMissingDensityFolder",
                "ObsoleteLintCustomCheck",
                "SetTextI18n"
    }
}
